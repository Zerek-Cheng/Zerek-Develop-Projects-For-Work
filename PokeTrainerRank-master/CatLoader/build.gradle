buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

import poketainerrank.ConfigureShadowRelocation

apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"
    mappings = "snapshot_20180609"
}

repositories {
    mavenLocal()
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    compile project(":Api")
    compileOnly project(":Bukkit")
    compile project(":Core")
    compile ('org.spongepowered:configurate-hocon:3.6') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    compile 'com.zaxxer:HikariCP:2.6.3'
    compile 'org.mariadb.jdbc:mariadb-java-client:2.0.3'
    compile 'com.h2database:h2:1.4.196'

    compile 'com.github.ben-manes.caffeine:caffeine:2.5.4'
    compile('com.github.ben-manes.caffeine:guava:2.5.4') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly files('./lib/CatServer-git-917005d-Community.jar')
    compileOnly "org.spigotmc:spigot:1.12.2"
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.ginyai.poketrainerrank.relocate"
    exclude 'cc.baka9'
    exclude 'net.ginyai'
    exclude 'com.pixelmonmod.pixelmon'
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

shadowJar {
    archiveName = "${parent.name}-Cat-${parent.version}.jar"
    destinationDir = parent.outputDir
//    manifest {
//        attributes 'FMLCorePlugin': 'net.ginyai.poketrainerrank.catloader.PokeTrainerRankCatLoader'
//        attributes 'FMLCorePluginContainsFMLMod': 'true'
//    }
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

build.dependsOn(shadowJar)