import poketainerrank.ConfigureShadowRelocation

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'ninja.miserable.blossom' version '1.0.1'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
}

group = parent.group
version = parent.version

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"
    mappings = "snapshot_20180609"
}

dependencies {
    compile project(":Api")
    compile project(":Core")
    compile ('org.spongepowered:configurate-hocon:3.6') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly 'org.spongepowered:spongeforge:1.12.2-2705-7.1.0-BETA-3361:dev'
    compileOnly "com.pixelmonmod:pixelmon:7.0.3:deobf"
    compileOnly ('com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT') {
        exclude group: 'org.spongepowered'
    } 
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

blossom {
    replaceToken '@version@', version
}

//del the mcmod.info generated by sponge api
task delMcmodInfo << {
    File file = new File(buildscript.sourceFile.getParent(),"build/classes/java/main/mcmod.info")
    if(file.exists()) {
        file.delete()
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.ginyai.poketrainerrank.relocate"
    exclude 'cc.baka9'
    exclude 'net.ginyai'
    exclude 'com.pixelmonmod.pixelmon'
}

shadowJar.dependsOn relocateShadowJar
build.dependsOn shadowJar
processResources.dependsOn delMcmodInfo

shadowJar {
    archiveName = "${parent.name}-Sponge-${parent.version}.jar"
    destinationDir = parent.outputDir
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}


